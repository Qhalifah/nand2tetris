class Hangman {
  field int bodyCenterX, headCenterY; // Used to position body parts
  // Track number of incorrect guesses
  field int maxWrong, currentWrong;
  
  constructor Hangman new() {
    let bodyCenterX = 150;
    let headCenterY = 110;
    let maxWrong = 6; // TODO: Make this higher?
    let currentWrong = 0; // Start fresh
    return this;
  }

  /** Draw one part of hanging man */
  method void drawNextBodyPart() {

    // TODO: Include eyes and mouth? Other parts?

    if (currentWrong = 1) {
      do Screen.drawCircle(bodyCenterX, headCenterY, 20); // Head
      do Screen.setColor(false);
      do Screen.drawCircle(bodyCenterX, headCenterY, 18); 
      do Screen.setColor(true); // Face
    }

    if (currentWrong = 2) {
      do Screen.drawLine(bodyCenterX, headCenterY + 20, bodyCenterX, headCenterY + 60); // Body
    }

    if (currentWrong = 3) {
      do Screen.drawLine(bodyCenterX - 30, headCenterY + 40, bodyCenterX, headCenterY + 40); // Left arm
    }

    if (currentWrong = 4) {
      do Screen.drawLine(bodyCenterX, headCenterY + 40, bodyCenterX + 30, headCenterY + 40); // Right arm
    }

    if (currentWrong = 5) {
      do Screen.drawLine(bodyCenterX - 30, headCenterY + 90, bodyCenterX, headCenterY + 60); // Left leg
    }

    if (currentWrong = 6) {
      do Screen.drawLine(bodyCenterX, headCenterY + 60, bodyCenterX + 30, headCenterY + 90); // Right leg
    }

    return;
  }

  /** User guessed wrong letter - add part to body */
  method void addPart() {
    let currentWrong = currentWrong + 1;
    do drawNextBodyPart();
    return;
  }

  method boolean allPartsDrawn() {
    return currentWrong = maxWrong;
  }
}
